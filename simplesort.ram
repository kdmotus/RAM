<input> 10 2 -2 7 1 0 67 -3 10 1 0
;
; R1: first register of the array
; R2: next register after last element of the array (R1 + N)
; R3: index of the external loop
; R4: last value for the index of external loop + 1
; R5: index of the internal loop
; R6: swap
; R7: first array element
;
	load	=7
	store	1
	read	0
	add	1
	store	2
	sub	=1
	store	4
	load	1
	store	3
;
; input data
;
INPUT:
	load	2
	sub	3
	jz	ENDIN
	read	0
	store	*3
	load	3
	add	=1
	store	3
	jmp	INPUT
ENDIN:
;
	load	1
	store	3
SORTLOOP:
	load	4
	sub	3
	jz	FINAL
	load	3
	add	=1
	store	5
INTLOOP:
	load	2
	sub	5
	jz	ILOOPEND
	load	*5
	sub	*3
	jgtz	NOSWAP
	load	*5
	store	6
	load	*3
	store	*5
	load	6
	store	*3
NOSWAP:
	load	5
	add	=1
	store	5
	jmp	INTLOOP
ILOOPEND:
	load	3
	add	=1
	store	3
	jmp	SORTLOOP
;
FINAL:
	load	1
	store	3
OUTPUT:
	load	2
	sub	3
	jz	END
	write	*3
	load	3
	add	=1
	store	3
	jmp	OUTPUT
;
END:
	halt